buildscript {
  ext {
    springBootVersion = '1.3.5.RELEASE'
    ratpackVersion = '1.3.3'
  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
  }
}

plugins {
  id "idea"
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.2.0'
}

group = "ru.ratauth"

allprojects {
  apply plugin: 'idea'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'provided-base'

  repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven { url "https://repo.spring.io/milestone" }
  }
  configurations {
    compile.exclude group: 'org.springframework.boot', module:'spring-boot-starter-tomcat'
  }

  dependencyManagement {
    imports {
      mavenBom 'io.spring.platform:platform-bom:2.0.5.RELEASE'
    }

    dependencies {
      //common
      dependency 'org.projectlombok:lombok:1.16.2'
      dependency 'org.apache.commons:commons-lang3:3.4'
      dependency 'com.google.guava:guava:19.0'
      dependency 'org.codehaus.groovy:groovy-all:2.4.7'//for ratpack server config
      dependency 'net.logstash.logback:logstash-logback-encoder:4.5.1'

      //spring
      dependencySet(group:'org.springframework.cloud', version:'1.0.2.RELEASE') {
        entry 'spring-cloud-config-client'
        entry 'spring-cloud-starter-eureka'
      }

      //reactive
      dependency 'io.reactivex:rxjava:1.1.6'
      dependency "io.ratpack:ratpack-rx:${ratpackVersion}"
      dependency "io.ratpack:ratpack-spring-boot:${ratpackVersion}"
      dependency "io.ratpack:ratpack-groovy:${ratpackVersion}"

      //crypto
      dependency 'com.nimbusds:nimbus-jose-jwt:4.20'

      //providers
      dependency 'ru.ratauth.providers:auth-mongo:+'
      dependency 'ru.ratauth.providers:auth-identity-mongo:+'
      dependency 'ru.alfabank.providers:provider-dca:0.+'

      //test
      dependency 'org.spockframework:spock-core:1.0-groovy-2.4'
      dependency 'org.spockframework:spock-spring:1.0-groovy-2.4'
      dependency "io.ratpack:ratpack-test:${ratpackVersion}"
      dependency 'org.springframework.restdocs:spring-restdocs-restassured:1.1.0.RELEASE'
      dependency  'org.springframework.restdocs:spring-restdocs-core:1.1.0.RELEASE'
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'
  apply plugin: 'jacoco'
  version = '0.3.5'
  group = 'ru.ratauth'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  jacocoTestReport {
    reports {
      xml.enabled true
      csv.enabled true
      html.destination "${buildDir}/jacocoHtml"
    }
  }

  jar {
    archiveName = project.name + ".jar"
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }
      }
    }
  }

  String repo = version.endsWith('SNAPSHOT') ? 'snapshots' : 'releases'
  artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
      repository {
        repoKey = repo
        username = "${artifactory_user}"
        password = "${artifactory_password}"
//        maven = true
      }
      defaults {
        publications('mavenJava')
      }
    }
    resolve {
      repository {
        repoKey = 'public'
        username = "${artifactory_user}"
        password = "${artifactory_password}"
        maven = true
      }
    }
  }

  dependencies {
    compile 'org.codehaus.groovy:groovy-all'
    compile 'org.projectlombok:lombok'
    compile 'io.reactivex:rxjava'

    //test
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-core', {
      exclude module: "groovy-all"
    }
    testCompile 'org.spockframework:spock-spring', {
      exclude module: "groovy-all"
    }
    testCompile 'io.ratpack:ratpack-test'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14'
}

idea {
  project {
    jdkName "1.8"
    languageLevel "1.8"
    vcs 'Git'
  }
}